plugins {
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
    id 'org.jreleaser' version '1.17.0'
}

version = '1.0.1'

dependencies {
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.18.3'
}

java {
    toolchain {
    	languageVersion = JavaLanguageVersion.of(21)
    }
    withSourcesJar()
    withJavadocJar()
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
    }
}

test {
    finalizedBy jacocoTestReport // report is always generated after tests run
}
jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
}

javadoc {
    failOnError = false
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.trquinn76'
            artifactId = 'classification-nzl'

            from components.java

            pom {
                name = 'New Zealand Classification Scheme'
                description = "Representation of New Zealand Protective Markings, which are immutable and suitable for use as DTO's"
                inceptionYear = "2025"
                url = "https://github.com/trquinn76/classification"
                licenses {
                    license {
                        name = 'MIT Licence'
                        url = 'https://github.com/trquinn76/classification/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'trqinn76'
                        name = 'Tim Quinn'
                        url = 'https://github.com/trquinn76'
                    }
                }
                scm {
                    url = 'http://github.com/trquinn/classification'
                    connection = 'scm:git:https://github.com/trquinn76/classification.git'
                    developerConnection = 'scm:git:ssh://github.com/trquinn76/classification.git'
                }
            }
        }
    }

    repositories {
        maven {
            url = layout.buildDirectory.dir('staging-deploy')
        }
    }
}

jreleaser {
    signing {
        active = 'ALWAYS'
        armored = true
    }
    deploy {
        maven {
            mavenCentral {
                sonatype {
                    active = 'ALWAYS'
                    url = 'https://central.sonatype.com/api/v1/publisher'
                    stagingRepository('build/staging-deploy')
                }
            }
        }
    }
}
